// Example created by Tom Mens, Software Engineering Lab, University of Mons, Belgium, December 2023

testclass CDPlayerTest for statechart CDPlayer {
	
	// test the statechart's initial configuration
	@Test
	operation testInitialStateConfiguration() {
		enter
		assert active(CDPlayer.Off)
	}
		
	@Test
	operation testActivatedCDPlayer() {
		enter
		raise on // turn on
		proceed 1 cycle  // process the event
		assert active(CDPlayer.On)
		assert active(CDPlayer.On.r1.Stop)
	}
	
	@Test
	operation testDeactivateCDPlayer() {
		enter
		raise on // turn on 
		proceed 1 cycle  // process the event
		raise off // turn off again
		proceed 1 cycle  // process the event
		assert active(CDPlayer.Off)
	}
	

	@Test
	operation testPlayPause() {
	    enter
		raise on // turn on
		proceed 1 cycle  // process the event
		raise play // to start playing 
		proceed 1 cycle  // process the event
		assert active(CDPlayer.On.r1.Play)
		raise pause // to pause playing 
		proceed 1 cycle  // process the event
		assert active(CDPlayer.On.r1.Pause)
		raise play // to start playing again
		proceed 1 cycle  // process the event
		assert active(CDPlayer.On.r1.Play)
	}
		
	@Test
	operation testPlayStop() {
	    enter
		raise on // turn on
		proceed 1 cycle  // process the event
		raise play // to start playing 
		proceed 1 cycle  // process the event
		assert active(CDPlayer.On.r1.Play)
		raise stop // to stop playing 
		proceed 1 cycle  // process the event
		assert active(CDPlayer.On.r1.Stop)
	}
	
	@Test
	operation testPlayPauseStop() {
	    enter
		raise on // turn on
		proceed 1 cycle  // process the event
		raise play // to start playing 
		proceed 1 cycle  // process the event
		assert active(CDPlayer.On.r1.Play)
		raise pause // to pause playing 
		proceed 1 cycle  // process the event
		assert active(CDPlayer.On.r1.Pause)
		raise stop // to stop playing 
		proceed 1 cycle  // process the event
		assert active(CDPlayer.On.r1.Stop)
	}
}

